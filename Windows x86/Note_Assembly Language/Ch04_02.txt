#4. Subprogram

<4.2 Subprogram Using JMP>
Method 1) Calculate return address VERY CAREFULLY.
Method 2) Use label as function pointer.

;====sub_JMP.asm====

%include "asm_io.inc"

segment .data
	msg_main_call 	db `main() is called.\n`, 0
	msg_main_retrun db `main() return 0.\n`, 0
	msg_sub_call 	db `sub() is called.\n`, 0
	msg_sub_return 	db `sub() return None.\n`, 0
	
segment .text
	global _main:
_main:
	enter 0, 0
	pusha
	
	mov eax, msg_main_call
	call print_string
	
	;====Method 1====
	mov ecx, $+7		;$ == (Current Address)
	jmp short _sub
	
	;mov ecx, $+10
	;jmp near _sub
	
	;====Method 2====
	mov ecx, ret_sub	;ECX = (Return Address)
	jmp short _sub		;call sub()
ret_sub:				;Return Address of sub()
	
	mov eax, msg_main_retrun
	call print_string
	
	popa
	mov eax, 0
	leave
	ret
	
_sub:
	;IMPORTANT!!
	;ECX should NOT be changed!
	
	mov eax, msg_sub_call
	call print_string
	
	;do something
	
	mov eax, msg_sub_return
	call print_string
	jmp ecx

Why?) JMP SHORT -> $+7, JMP NEAR -> $+10:
Consider the length(?) of the Instructions!
	MOV r/m8  = 2
	MOV r/m16 = 4
	MOV r/m32 = 5
	JMP SHORT label = 2
	JMP NEAR  label = 5
	Tip) nasm -f elf myfile.asm -l myfile.lst
	
	Instruction		Address		Length(?)
	mov ecx, $+7	a			5
	jmp short _sub	a+5			2
	;next inst.		a+7
	
	mov ecx, $+10	a			5
	jmp near _sub	a+5			5
	;next inst.		a+10